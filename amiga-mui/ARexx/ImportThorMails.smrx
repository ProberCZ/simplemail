/*
This is an ARexx-Script for SimpleMail

$VER: ImportThorMails.smrx 1.6 (26.2.10)
$Author: AmigaPhil
$Label: Import messages from Thor
$Desc: Import Thor's selected messages into SimpleMail

Contact: AmigaPhil <AmigaPhil (at) scarlet (dot) be>

Translation credits:
- french catalog by AmigaPhil
- dutch catalog by Johan Banis - ATO Nederland
- italian catalog by Simone "Tuxedo" Monsignori
*/

signal on halt
signal on break_c
options results
options failat 31

version  = SUBWORD(SOURCELINE(4),3,1)
wrapat = 60   /* Word-wrap a line of text to the given size in chars */

/* Localize (RexCat) */

langFile = ""
if exists('ENV:Language') then
   do
   if open('el', 'ENV:Language', 'R') then
      do
      langFile = readln('el')
      call close('el')
      end
   end
call gettlstext(langFile)

/* check if SimpleMail is running */

smport = address()
if left(smport,11) ~== "SIMPLEMAIL." then
   do
   address command
   'WHICH >NIL: REQUESTCHOICE'
   if rc = 5 then say tls("Error: ") || "ImportThorMails.smrx" || tls(" must be run from SimpleMail.")
   else 'RUN >NIL: REQUESTCHOICE >NIL: "ImportThorMails.smrx" "' || tls("This script must be run from SimpleMail.") || '" "' || tls("Ok") || '"'
   exit
   end
address(smport)
'VERSION'
if symbol('result') ~= 'VAR' then smver = 0
else do
   smver = result
   parse var smver yver"."yrev
   vt = verify(yver,"0123456789")
   if vt > 0 then yver = left(yver,vt-1)
   vt = verify(yrev,"0123456789")
   if vt > 0 then yrev = left(yrev,vt-1)
   smver = (yver * 65536) + yrev
end

help = "*Ec*EuImportThorMails.smrx*En v" || version || "*n*n*El"
help = help || tls("This utility import selected messages in Thor (or the current one) into SimpleMail. ")
'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Continue") || '|' || tls("Quit") || '"'
if result ~= 1 then exit

p = ' ' || address() || ' ' || show('P',,)
thorport = pos(' THOR.',p)
if thorport = 0 then
   do
   help = tls("Error: ") || tls("Cannot find Thor port.") || "*n"
   help = help || tls("Please start Thor and select the messages you want to import from Thor's message list.")
   'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Ok") || '"'
   exit
   end
thorport = word(substr(p,thorport+1),1)

if ~show('p', 'BBSREAD') then
   do
   address command
   "run >nil: `GetEnv THOR/THORPath`bin/LoadBBSRead"
   "WaitForPort BBSREAD"
   end
if ~show('p', 'BBSREAD') then
   do
   address(smport)
   help = tls("Error: ") || tls("Cannot load BBSREAD library.") || "*n"
   help = help || tls("Please check your Thor installation.")
   'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Cancel") || '"'
   exit
   end


address(thorport)
'VERSION STEM 'VERS
parse var VERS.THOR tver"."trev
vt = verify(tver,"0123456789")
if vt > 0 then tver = left(tver,vt-1)
vt = verify(trev,"0123456789")
if vt > 0 then trev = left(trev,vt-1)
thorver = (tver * 65536) + trev
if thorver < 131073 then
   do
   address(smport)
   help = tls("Sorry, this script needs at least Thor v2.1.")
   'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Ok") || '"'
   exit
   end

'GETMSGLISTSELECTED STEM 'MSGLIST
if rc = 3 then
   do
   'CURRENTMSG 'CURMSG
   if (rc = 30) & (THOR.LASTERROR = 'No current message.') then
      do
      address(smport)
      help = tls("No message selected and no current message.") || "*n"
      help = help || tls("Please select the messages you want to import from Thor's message list.")
      'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Ok") || '"'
      exit
      end
   else
      do
      if rc ~= 0 then
         do
         address(smport)
         help = tls("Error: ") || THOR.LASTERROR
         'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Cancel") || '"'
         exit
         end
      else
         do
         MSGLIST.1 = CURMSG.MSGNR; MSGLIST.COUNT = 1
         end
      end
   drop CURMSG.
   end
else if rc ~= 0 then
   do
   address(smport)
   if rc = 5 then
      do
      help = tls("No message selected and no current message.") || "*n"
      help = help || tls("Please select the messages you want to import from Thor's message list.")
      'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Ok") || '"'
      exit
      end
   else
      do
      help = THOR.LASTERROR || "*n"
      help = help || tls("Please select the messages you want to import from Thor's message list.")
      'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Ok") || '"'
      exit
      end
   end
'CURRENTSYSTEM STEM 'CURSYS
if rc ~= 0 then
   do
   address(smport)
   help = tls("Error: ") || THOR.LASTERROR
   'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Cancel") || '"'
   exit
   end
else if CURSYS.CONFNAME = "" then
   do
   address(smport)
   help = tls("Error: ") || tls("Current conference name not found.")
   'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Cancel") || '"'
   exit
   end
address(smport)
help = tls("Please select the folder to import the messages into:")
'REQUESTFOLDER BODY "'smwrap(help,wrapat)'"'
if rc ~= 0 then exit
smfolder = result
if MSGLIST.COUNT = 1 then help = tls("The current message is ready to import.")
else help = MSGLIST.COUNT || tls(" messages ready to import.")
help = tls("Importing messages from ") || "*EbThor:" || CURSYS.CONFNAME || "*En" || tls(" to ") || "*EbSimpleMail:" || smfolder || "*En :*n*n" || help
'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Import") || '|' || tls("Cancel") || '"'
if result ~= 1 then exit
'MAILLISTFREEZE'

window = ""
address(thorport)
'OPENPROGRESS TITLE "' || tls("Importing messages from ") || CURSYS.CONFNAME || '" TOTAL 'MSGLIST.COUNT' AT "' || tls("_Abort") || '"'
if rc = 0 then window = result

do i=1 to MSGLIST.COUNT
   address(thorport)
   if window ~= "" then
      do
      'UPDATEPROGRESS REQ 'window' CURRENT 'i' PT "' || tls("Importing message #") || MSGLIST.i || '"'
      if rc ~= 0 then
         do
         if rc = 5 then leave i
         else
            do
            'CLOSEPROGRESS REQ 'window
            address(smport)
            help = tls("Error: ") || THOR.LASTERROR
            'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Cancel") || '"'
            exit
            end
         end
      end
   drop MSGTXT. MSGDAT.
   address(BBSREAD)
   'READBRMESSAGE "'CURSYS.BBSNAME'" "'CURSYS.CONFNAME'" 'MSGLIST.i' TEXTSTEM MSGTXT DATASTEM MSGDAT'
   if rc ~= 0 then
      do
      address(thorport)
      'CLOSEPROGRESS REQ 'window
      address(smport)
      help = tls("Error: ") || BBSREAD.LASTERROR
      'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Cancel") || '"'
      exit
      end
   call fixbin('MSGTXT')

   address(thorport)
   'SAVEMESSAGE BBS "'CURSYS.BBSNAME'" CONF "'CURSYS.CONFNAME'" MSGNR 'MSGLIST.i' FILE "T:ThorSavedMsg.tmp" OVERWRITE'
   if rc ~= 0 then
      do
      say tls("Failed to save message #") || MSGLIST.i || " (rc=" || rc || "): " || THOR.LASTERROR
      say ""
      end
   else
      do
      address(smport)
      'MAILADD FOLDER "'smfolder'" FILENAME "T:ThorSavedMsg.tmp"'
      impMsg = result
      if smver > 33 then
         do
         setStat = ""
         if bittst(MSGDAT.FLAGS, 1) then setStat = "R"
         else if ~(bittst(MSGDAT.FLAGS, 10) | bittst(MSGDAT.FLAGS, 0)) then setStat = "O"
         else setStat = "U"
         if setStat ~= "" then
            do
            if verify(impMsg,":/") = 0 then
               do
               'FOLDERINFO "'smfolder'" STEM 'FINFO.
               if (right(FINFO.PATH,1) ~= ":") & (right(FINFO.PATH,1) ~= "/") then FINFO.PATH = FINFO.PATH || "/"
               impMsg = FINFO.PATH || impMsg
               end
            'MAILSETSTATUS "'impMsg'" 'setStat
            end
         end
      end

end


halt:
break_c:

if BINLIST.COUNT > 0 then
   do
   address command 'DELETE >NIL: "'tmpmfile'"'
    do j=1 to BINLIST.COUNT
       'DELETE >nil: "'BINLIST.j'"'
    end
   end

if exists("T:ThorSavedMsg.tmp") then address command 'DELETE "T:ThorSavedMsg.tmp" QUIET'

address(smport)
'MAILLISTTHAW'

if window ~= "" then
   do
   address(thorport)
   'CLOSEPROGRESS REQ 'window
   end

exit



/* Word-wrap a text string to the given size per line.
   A size below 20 chars will be ignored (no wrap).    */

smwrap: procedure

parse arg intext,wrap
if ~datatype(wrap,'W') then wrap = 0
if (wrap > 19) & (words(intext) > 1) then
   do
   tmptext = ""
   tmpline = word(intext,1)
     do i = 2 to words(intext)
     nextword = word(intext,i)
     if length(tmpline || " " || nextword) > wrap then
        do
        tmptext = tmptext || tmpline || "*n"
        tmpline = nextword
        end
     else
        do
        tmpline = tmpline || " " || nextword
        lastLF = lastpos("*N",upper(tmpline))
        if lastLF > 1 then
           do
           tmptext = tmptext || substr(tmpline,1,lastLF+1)
           tmpline = substr(tmpline,lastLF+2)
           end
        end
     end
   if length(tmpline) > 0 then tmptext = tmptext || tmpline
   intext = tmptext
   end
return intext


/* Replace a missing file by a dummy one.  This is needed
   to avoid a warning requester popping up when a binary
   attachment has been cropped from the message.
   (Check recursively in message parts.)                    */

fixbin: procedure expose MSGTXT. BINLIST.

parse arg tstem

if symbol('BINLIST.COUNT') ~= 'VAR' then BINLIST.COUNT = 0
if (symbol(tstem'.PART.COUNT') = 'VAR') then
   do
   parts = value(tstem'.PART.COUNT')
   if parts > 0 then
      do i = 1 to parts
      if (symbol(tstem'.PART.'i'.BINARY') = 'VAR') then
         do
         file = escwc(value(tstem'.PART.'i'.BINARY'))
         if file ~= "" then
            do
            if ~exists(file) then
               do
               cnt = BINLIST.COUNT
               call open('dummy',file,'W')
               call writech('dummy','[attachment deleted]')
               call close('dummy')
               cnt = cnt + 1
               BINLIST.cnt = file
               BINLIST.COUNT = cnt
               end
            end
         end
      else
         do
         usestem = tstem'.PART.'i'.MSG'
         call fixbin(usestem)
         end
      end
   end

return


/* Escape wildcards characters in filenames */

escwc: procedure

   parse arg instr
   if length(instr) < 1 then return instr

   /* Wilcard characters in AmigaDOS: */
   /* #?|()~[]% plus the single quote */

   outstr = ""

   do i=1 to length(instr)
     tstchr = substr(instr,i,1)
     select
        when tstchr = "#" then outstr = outstr || "'" || tstchr
        when tstchr = "?" then outstr = outstr || "'" || tstchr
        when tstchr = "|" then outstr = outstr || "'" || tstchr
        when tstchr = "(" then outstr = outstr || "'" || tstchr
        when tstchr = ")" then outstr = outstr || "'" || tstchr
        when tstchr = "~" then outstr = outstr || "'" || tstchr
        when tstchr = "[" then outstr = outstr || "'" || tstchr
        when tstchr = "]" then outstr = outstr || "'" || tstchr
        when tstchr = "%" then outstr = outstr || "'" || tstchr
        when tstchr = "'" then outstr = outstr || "'" || tstchr
        otherwise outstr = outstr || tstchr
     end
   end

return outstr


/* (RexCat) Function which translates the strings */

tls: PROCEDURE EXPOSE tlstext.

  PARSE ARG instring

  IF instring = '' THEN RETURN instring
  IF LENGTH(instring) > 250 THEN instring = LEFT(instring,250)
  IF tlstext.instring == 'TLSTEXT.' || instring THEN RETURN instring
  IF tlstext.instring = '' THEN RETURN instring

  RETURN tlstext.instring


/* (RexCat) Function to load the translation file */

gettlstext: PROCEDURE EXPOSE tlstext.

  PARSE ARG language
  IF language = '' THEN RETURN 0

  currentDir = PRAGMA('D')
  IF (RIGHT(currentDir,1) ~= ':') & (RIGHT(currentDir,1) ~= '/') THEN currentDir = currentDir || "/"
  scriptDir  = ''
  PARSE SOURCE scriptPath
  scriptPath = SUBWORD(scriptPath,4,WORDS(scriptPath)-5)
  parpos = LASTPOS('/', scriptPath)
  IF parpos = 0 THEN parpos = POS(':', scriptPath)
  IF parpos > 0 THEN scriptDir = LEFT(scriptPath, parpos)
  scriptName = SUBSTR(scriptPath, parpos + 1)
  langFile = language || "/" || scriptName || ".catalog"
  CALL PRAGMA('W','N')
  IF EXISTS("LOCALE:RexxCatalogs") THEN CALL PRAGMA('D',"LOCALE:RexxCatalogs")
  CALL PRAGMA('W','W')
  IF ~EXISTS(langFile) THEN CALL PRAGMA('D',currentDir || "RexxCatalogs")
  IF ~EXISTS(langFile) THEN CALL PRAGMA('D',currentDir || "Locale/Catalogs")
  IF ~EXISTS(langFile) THEN CALL PRAGMA('D',scriptDir || "RexxCatalogs")
  IF ~EXISTS(langFile) THEN CALL PRAGMA('D',scriptDir || "Locale/Catalogs")

  rcode = 0
  IF OPEN('lf',langFile, 'R') THEN DO
    rcode = 1
    tlssrc = ''
    tlsdst = ''
    DO WHILE ~EOF('lf')
      instring = STRIP(READLN('lf'))
      IF (LENGTH(instring) > 0) & (LEFT(instring,2) ~= '/*') & (LEFT(instring,2) ~= '//') THEN DO
        PARSE VAR instring tmpvar "=" qtmpstr
        IF (TRANSLATE(STRIP(tmpvar)) ~= "TLSSRC") & (TRANSLATE(STRIP(tmpvar)) ~= "TLSDST") THEN ITERATE
        tmpstr = STRIP(qtmpstr,'L')
        quot = LEFT(tmpstr,1)
        IF (quot = "'") | (quot = '"') THEN DO
           strend = POS(quot,tmpstr,2)
           IF strend > 1 THEN DO
              tmpstr = SUBSTR(tmpstr,2,strend - 2)
              IF LENGTH(tmpstr) > 250 THEN tmpstr = LEFT(tmpstr,250)
              INTERPRET tmpvar || "=" || quot || tmpstr || quot
           END
        END
      END
      IF tlssrc ~= '' THEN DO
        IF tlsdst ~= '' THEN DO
          tlstext.tlssrc = tlsdst
          tlssrc = ''
          tlsdst = ''
        END
      END
    END
    CALL CLOSE('lf')
  END

  CALL PRAGMA('D',currentDir)

  RETURN rcode


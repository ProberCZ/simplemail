/*
This is an ARexx-Script for SimpleMail

$VER: ImportYamMails.smrx 1.5 (19.2.10)
$Author: AmigaPhil
$Label: Import messages from YAM
$Desc: Import YAM's selected messages into SimpleMail

Contact: AmigaPhil <AmigaPhil (at) scarlet (dot) be>

Translation credits:
- french catalog by AmigaPhil
- dutch catalog by Johan Banis - ATO Nederland
- italian catalog by Simone "Tuxedo" Monsignori
*/

signal on halt
signal on break_c
options results
options failat 31

version  = SUBWORD(SOURCELINE(4),3,1)
wrapat = 60   /* Word-wrap a line of text to the given size in chars */

/* Localize (RexCat) */

langFile = ""
if exists('ENV:Language') then
   do
   if open('el', 'ENV:Language', 'R') then
      do
      langFile = readln('el')
      call close('el')
      end
   end
call gettlstext(langFile)

/* check if SimpleMail is running */

smport = address()
if left(smport,11) ~== "SIMPLEMAIL." then
   do
   address command
   'WHICH >NIL: REQUESTCHOICE'
   if rc = 5 then say tls("Error: ") || "ImportYamMails.smrx" || tls(" must be run from SimpleMail.")
   else 'RUN >NIL: REQUESTCHOICE >NIL: "ImportYamMails.smrx" "' || tls("This script must be run from SimpleMail.") || '" "' || tls("Ok") || '"'
   exit
   end
address(smport)
'VERSION'
if symbol('result') ~= 'VAR' then smver = 0
else do
   smver = result
   parse var smver yver"."yrev
   vt = verify(yver,"0123456789")
   if vt > 0 then yver = left(yver,vt-1)
   vt = verify(yrev,"0123456789")
   if vt > 0 then yrev = left(yrev,vt-1)
   smver = (yver * 65536) + yrev
end

help = "*Ec*EuImportYamMails.smrx*En v" || version || "*n*n*El"
help = help || tls("This utility import selected messages in YAM (or the current one) into SimpleMail.")
'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Continue") || '|' || tls("Quit") || '"'
if result ~= 1 then exit

if ~show('P','YAM') then
   do
   help = tls("Error: ") || tls("Cannot find YAM port.") || "*n"
   help = help || tls("Please start YAM and select the messages you want to import from YAM's message list.")
   'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Ok") || '"'
   exit
   end
yamport = "YAM"

address(yamport)
'INFO VERSION'
yamver = word(result,3)
parse var yamver yver"."yrev
vt = verify(yver,"0123456789")
if vt > 0 then yver = left(yver,vt-1)
vt = verify(yrev,"0123456789")
if vt > 0 then yrev = left(yrev,vt-1)
yamver = (yver * 65536) + yrev

'GETSELECTED STEM 'MSGLIST.
if result = 0 then
   do
   address(smport)
   help = tls("No message selected.") || "*n"
   help = help || tls("Please select the messages you want to import from YAM's message list.")
   'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Ok") || '"'
   exit
   end
'FOLDERINFO'
yamfolder = word(result,2,1)
address(smport)
help = tls("Please select the folder to import the messages into:")
'REQUESTFOLDER BODY "'smwrap(help,wrapat)'"'
if rc ~= 0 then exit
smfolder = result
if MSGLIST.NUM.COUNT = 1 then help = tls("The current message is ready to import.")
else help = MSGLIST.NUM.COUNT || tls(" messages ready to import.")
help = tls("Importing messages from ") || "*EbYAM:" || yamfolder || "*En" || tls(" to ") || "*EbSimpleMail:" || smfolder || "*En :*n*n" || help
'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Import") || '|' || tls("Cancel") || '"'
if result ~= 1 then exit
say tls("Importing messages from YAM...")
'MAILLISTFREEZE'

address(yamport)
'APPBUSY "' || tls("Exporting messages to SimpleMail...") || '"'
'LISTSELECT N'
if yamver > 131077 then 'LISTFREEZE MAILS'

do i=0 to (MSGLIST.NUM.COUNT - 1)
   address(yamport)
   'SETMAIL 'MSGLIST.NUM.i
   if yamver < 131078 then 'MAILEXPORT "T:YAMmbox.tmp"'
   else 'MAILEXPORT "T:YAMmbox.tmp" QUIET'

   if ~open('mb',"T:YAMeml.tmp",'W') then
      do
      help = tls("Error: ") || tls("Cannot write to the YAMeml temp. file.")
      'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Cancel") || '"'
      signal break_error
      end
   if ~open('tmp',"T:YAMmbox.tmp",'R') then
      do
      help = tls("Error: ") || tls("Cannot read the YAMmbox temp. file.")
      'REQUEST BODY "'smwrap(help,wrapat)'" "' || "**" || tls("Cancel") || '"'
      signal break_error
      end
   isHeader = 1
   setStat = ""
   do while ~eof('tmp')
      instring = readln('tmp')
      if isHeader then
         do
         if upper(left(instring,5)) == "FROM " then iterate
         if (upper(left(instring,8)) == "STATUS: ") & (setStat = "") then
            do
            instring = substr(instring,9)
            if verify(instring,'R','M') > 0 then setStat = "O"
            else if verify(instring,'O','M') > 0 then setStat = "U"
            iterate
            end
         if upper(left(instring,10)) == "X-STATUS: " then
            do
            instring = substr(instring,11)
            if verify(instring,'A','M') > 0 then setStat = "R"
            iterate
            end
         if strip(instring,'T','0d'x) = "" then isHeader = 0
         end
      testfrom = strip(word(instring,1),'L','>')
      if testfrom == "From" then instring = ">" || instring
      call writeln('mb',instring)
   end
   call close('tmp')
   call close('mb')

   address(smport)
   'MAILADD FOLDER "'smfolder'" FILENAME "T:YAMeml.tmp"'
   impMsg = result
   if (smver > 33) & (setStat ~= "") then
      do
      if verify(impMsg,":/") = 0 then
         do
         'FOLDERINFO "'smfolder'" STEM 'FINFO.
         if (right(FINFO.PATH,1) ~= ":") & (right(FINFO.PATH,1) ~= "/") then FINFO.PATH = FINFO.PATH || "/"
         impMsg = FINFO.PATH || impMsg
         end
      'MAILSETSTATUS "'impMsg'" 'setStat
      end
end
say tls("Done.")

halt:
break_c:
break_error:

if exists("T:YAMmbox.tmp") then address command 'DELETE >NIL: "T:YAMmbox.tmp"'
if exists("T:YAMeml.tmp") then address command 'DELETE >NIL: "T:YAMeml.tmp"'
address(yamport)
if yamver > 131077 then 'LISTUNFREEZE MAILS'
'APPNOBUSY'
address(smport)
'MAILLISTTHAW'

exit



/* Word-wrap a text string to the given size per line.
   A size below 20 chars will be ignored (no wrap).    */

smwrap: procedure

parse arg intext,wrap
if ~datatype(wrap,'W') then wrap = 0
if (wrap > 19) & (words(intext) > 1) then
   do
   tmptext = ""
   tmpline = word(intext,1)
     do i = 2 to words(intext)
     nextword = word(intext,i)
     if length(tmpline || " " || nextword) > wrap then
        do
        tmptext = tmptext || tmpline || "*n"
        tmpline = nextword
        end
     else
        do
        tmpline = tmpline || " " || nextword
        lastLF = lastpos("*N",upper(tmpline))
        if lastLF > 1 then
           do
           tmptext = tmptext || substr(tmpline,1,lastLF+1)
           tmpline = substr(tmpline,lastLF+2)
           end
        end
     end
   if length(tmpline) > 0 then tmptext = tmptext || tmpline
   intext = tmptext
   end
return intext


/* (RexCat) Function which translates the strings */

tls: PROCEDURE EXPOSE tlstext.

  PARSE ARG instring

  IF instring = '' THEN RETURN instring
  IF LENGTH(instring) > 250 THEN instring = LEFT(instring,250)
  IF tlstext.instring == 'TLSTEXT.' || instring THEN RETURN instring
  IF tlstext.instring = '' THEN RETURN instring

  RETURN tlstext.instring


/* (RexCat) Function to load the translation file */

gettlstext: PROCEDURE EXPOSE tlstext.

  PARSE ARG language
  IF language = '' THEN RETURN 0

  currentDir = PRAGMA('D')
  IF (RIGHT(currentDir,1) ~= ':') & (RIGHT(currentDir,1) ~= '/') THEN currentDir = currentDir || "/"
  scriptDir  = ''
  PARSE SOURCE scriptPath
  scriptPath = SUBWORD(scriptPath,4,WORDS(scriptPath)-5)
  parpos = LASTPOS('/', scriptPath)
  IF parpos = 0 THEN parpos = POS(':', scriptPath)
  IF parpos > 0 THEN scriptDir = LEFT(scriptPath, parpos)
  scriptName = SUBSTR(scriptPath, parpos + 1)
  langFile = language || "/" || scriptName || ".catalog"
  CALL PRAGMA('W','N')
  IF EXISTS("LOCALE:RexxCatalogs") THEN CALL PRAGMA('D',"LOCALE:RexxCatalogs")
  CALL PRAGMA('W','W')
  IF ~EXISTS(langFile) THEN CALL PRAGMA('D',currentDir || "RexxCatalogs")
  IF ~EXISTS(langFile) THEN CALL PRAGMA('D',currentDir || "Locale/Catalogs")
  IF ~EXISTS(langFile) THEN CALL PRAGMA('D',scriptDir || "RexxCatalogs")
  IF ~EXISTS(langFile) THEN CALL PRAGMA('D',scriptDir || "Locale/Catalogs")

  rcode = 0
  IF OPEN('lf',langFile, 'R') THEN DO
    rcode = 1
    tlssrc = ''
    tlsdst = ''
    DO WHILE ~EOF('lf')
      instring = STRIP(READLN('lf'))
      IF (LENGTH(instring) > 0) & (LEFT(instring,2) ~= '/*') & (LEFT(instring,2) ~= '//') THEN DO
        PARSE VAR instring tmpvar "=" qtmpstr
        IF (TRANSLATE(STRIP(tmpvar)) ~= "TLSSRC") & (TRANSLATE(STRIP(tmpvar)) ~= "TLSDST") THEN ITERATE
        tmpstr = STRIP(qtmpstr,'L')
        quot = LEFT(tmpstr,1)
        IF (quot = "'") | (quot = '"') THEN DO
           strend = POS(quot,tmpstr,2)
           IF strend > 1 THEN DO
              tmpstr = SUBSTR(tmpstr,2,strend - 2)
              IF LENGTH(tmpstr) > 250 THEN tmpstr = LEFT(tmpstr,250)
              INTERPRET tmpvar || "=" || quot || tmpstr || quot
           END
        END
      END
      IF tlssrc ~= '' THEN DO
        IF tlsdst ~= '' THEN DO
          tlstext.tlssrc = tlsdst
          tlssrc = ''
          tlsdst = ''
        END
      END
    END
    CALL CLOSE('lf')
  END

  CALL PRAGMA('D',currentDir)

  RETURN rcode

